<!DOCTYPE html>
<html lang="en">

<title>PS70: Intro to Digital Fabrication </title>
<link href="https://cdn.jsdelivr.net/npm/bootstrap@5.1.1/dist/css/bootstrap.min.css" rel="stylesheet">
  <link href="./style.css" rel="stylesheet">
  <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/arduino-light.min.css">
  <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.8.0/highlight.min.js"></script>
  <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/languages/arduino.min.js"></script>
  <script>hljs.initHighlightingOnLoad();</script> 

<nav class="navbar navbar-expand-sm navbar-light bg-light">
  <div style="align-items: left; justify-content:left;" class="container-fluid">
    <h3 class="nav-title"> PS70: Intro to Digital Fabrication</h3>
    <div class="navbar-nav">
      <h4><a class="nav-link" href="./index.html">Home</a></h4>
      <h4><a class="nav-link" href="./about.html">About</a></h4>
      <h4><a class="nav-link" href="./final_project.html">Final Project</a></h4>
    </div>
  </div>
</nav>

<body >

<xmp style="display:none;">
### Final Project
<p>I've decided to pursue the card detecting wallet as my project!</p>

<h3 style="font-weight: bold;">Week 11 Update: Constructing the Wallet</h3>
<p>This week I focused on constructing the wallet. I first worked with burlap or canvas material. Then, I realized that the cloth made sticking the copper plates rather difficult. Instead I decided to use gaffer tape so that the surface could be more firm to adhere to the copper plate.</p>

<p>Goals</p>
<ul>
  <li>Sauder the wires on the breadboard</li>
  <li>Create the plates on the wallet</li>
  <li>Reach goal - get resistors so I can have multiple cards per slot</li>
</ul>

<h3 style="font-weight: bold;">Week 10 Update: Moving Away from RFID and Towards Reading Multiple Cards</h3>
<p>After more conversation with the teaching staff, I realized that the RFID approach will not be feasible to expand to many cards given the hefty pricetag for a simultaneous RFID sensor. Instead, I pivoted to creating a circuit that is completed by copper plates. The idea is that each card will have a copper strip and its own slot that's connected to a particular pin on the ESP 32 Dev Board. When the card is in the slot, the circuit for that slot would be complete, and we could detect which slots have or don't have a card on the Dev Board.</p>

<p>For the programming, I expanded the initial approach for multiple variables so that each slot has its own state (await, pending, or alarm) and will notify independently when the card is missing after the designated duration through the chat bot. I was also able to simplify the programming so that I can simultaneously preset each of the slots as either having a card present or not. My current set up includes two slots as seen below:</p>

<image width="100%" height="75%" src="./10_machine_building/Copper.jpg" alt="CNC"></image>

<p>Notice how the circuit is completed with the "card" present to complete the circuit on the top pair of copper plates. I also maintain the presence of the button to change through the states.</p>
<p>My code is below as well:</p>
<pre><code>
  // https://github.com/mathertel/OneButton
  #include &lt;OneButton.h&gt;
  #include &lt;WiFi.h&gt;
  #include &lt;HTTPClient.h&gt;
  #include &lt;UrlEncode.h&gt;
  
  const char* ssid = "MAKERSPACE";
  const char* password = "12345678";
  
  // States of the machine
  #define AWAIT 0
  #define SETTING 1
  #define PENDING 2
  #define ALARM 3
  
  // Pins for the button and cards
  #define BUTTON_PIN 0
  
  unsigned long curr_time = 0;
  // Reads 1 when disconnected, 0 when connected
  bool is_setting = false;
  int card_pins[2] = {1, 2};
  int reference[2] = {1, 1};
  int states[2] = {AWAIT};
  unsigned long missing_times[2] = {NULL};
  #define array_size sizeof(card_pins)/sizeof(int)
  
  // Button initialization
  OneButton btn = OneButton(
    BUTTON_PIN,  // Input pin for the button
    true,      
    true
  );
  
  // Handler function for a single click:
  static void handleClick() {
    is_setting = false;
  }
  
  static void doubleClick() {
    is_setting = true;
  }
  
  void sendMessage(String message){
    // Data to send with HTTP POST
    String url = "https://api.callmebot.com/whatsapp.php?phone=" + phoneNumber + "&apikey=" + apiKey + "&text=" + urlEncode(message);    
    HTTPClient http;
    http.begin(url);
  
    // Specify content-type header
    http.addHeader("Content-Type", "application/x-www-form-urlencoded");
    
    // Send HTTP POST request
    int httpResponseCode = http.POST(url);
    if (httpResponseCode == 200){
      Serial.print("Message sent successfully");
    }
    else{
      Serial.println("Error sending the message");
      Serial.print("HTTP response code: ");
      Serial.println(httpResponseCode);
    }
  
    // Free resources
    http.end();
  }
  
  void setup() {
    Serial.begin(9600);
  
    for (int i=0; i<array_size; i++) {
      pinMode(card_pins[i], INPUT_PULLUP);
    }
  
    // WiFi Setup
    WiFi.begin(ssid, password);
    Serial.println("Connecting");
    while(WiFi.status() != WL_CONNECTED) {
      delay(500);
      Serial.print(".");
    }
    Serial.println("");
    Serial.print("Connected to WiFi network with IP Address: ");
    Serial.println(WiFi.localIP());
  
    // Single Click event attachment
    btn.attachClick(handleClick);
  
    // Double Click event attachment with lambda
    btn.attachDoubleClick(doubleClick);
  }
  
  void checkState(int i) {
    Serial.print("READ ");
    Serial.print(String(digitalRead(card_pins[i])) + " ");
    Serial.println(reference[i]);
    Serial.println((digitalRead(card_pins[i]) != reference[i]));
  
    if ((digitalRead(card_pins[i]) != reference[i]) && (reference[i] != 1)) {
      if (states[i] == AWAIT) {
        states[i] = PENDING;
        missing_times[i] = millis();
      }
    }
    else {
      states[i] = AWAIT;
    }
  }
  
  void checkTime(int i) {
    if ((states[i] == PENDING) && (millis() - missing_times[i]) > 3000) {
      states[i] = ALARM;
      missing_times[i] = millis();
    }
  }
  
  void setState() {
    Serial.println("SETTING!");
    for (int i = 0; i < array_size; i++) {
      reference[i] = digitalRead(card_pins[i]);
    }
  }
  
  void soundAlarm(int id) {
    sendMessage("Your card in slot " + String(id) + " is missing!");
    states[id] = PENDING;
    missing_times[id] = millis();
  }
  
  
  void loop() {
    // Activate button
    btn.tick();
  
    if (is_setting) {
      setState();
    }
    else {
      for (int i = 0; i < array_size; i++ ) {
        Serial.println("STATE " + String(i) + " " + String(states[i]));
        if (states[i] == AWAIT) {
          Serial.println("Awating!!");
          checkState(i);
        }
        else if (states[i] == PENDING) {
          // Check whether we've detected the OG card yet
          Serial.println("Pending!!");
          Serial.println(curr_time);
          checkState(i);
          // Check the time situation
          checkTime(i);
        }
        else if (states[i] == ALARM) {
          soundAlarm(i);
        }
      }
    }
  }  
</code></pre>



<h3 style="font-weight: bold;">Week 9 Update: Incorporating ESP32 and Messaging Notification</h3>
<p>To address the long-term goal of the project, I didn't want a buzzer to go off everytime you lost your wallet so I switched the board over to ESP32 and had the project message you to alarm you of your missing card instead.</p>

<p>I referenced <a href="https://randomnerdtutorials.com/esp32-send-messages-whatsapp/">the documentation on sending a message</a>. I set up my Whatsapp with the Chat Bot and ported the setup code in the documentation into my current code.</p>

<p>Also, I switched from the Arduino Uno to the ESP32 C3 Dev Board which required figuring out which ports to use for MOSI and MISO of the RFID. My current pins from the RFID to the ESP32 are as follows:</p>
<ul>
  <li>GND to GND</li>
  <li>RST to 8</li>
  <li>3.3V to 3.3V</li>
  <li>SDA to 10</li>
  <li>SCK to 4</li>
  <li>MOSI to 6</li>
  <li>MISO to 5</li>
</ul>
  
<p>After adjusting those, I was able to have the same setup work but notifications done through a message. A demo is below:</p>

<video width="50%" height="100%" controls><source src="./09_radio_wifi_bluetooth/esp_rfid.mp4" type="video/mp4"></video>

<p>My code at this moment is below:</p>
<pre><code>
#include &lt;SPI.h&gt;
// https://arduinogetstarted.com/tutorials/arduino-rfid-nfc
#include &lt;MFRC522.h&gt;
// https://github.com/mathertel/OneButton
#include &lt;OneButton.h&gt;

#include &lt;WiFi.h&gt;  
#include &lt;HTTPClient.h&gt;
#include &lt;UrlEncode.h&gt;

const char* ssid = "MAKERSPACE";
const char* password = "12345678";

String phoneNumber = "";
String apiKey = "";

#define SS_PIN 10
#define RST_PIN 8
#define LED_PIN 2
#define BUTTON_PIN 3
#define BUZZER_PIN 2
#define AWAIT 0
#define SETTING 1
#define PENDING 2
#define ALARM 3

void sendMessage(String message){

  // Data to send with HTTP POST
  String url = "https://api.callmebot.com/whatsapp.php?phone=" + phoneNumber + "&apikey=" + apiKey + "&text=" + urlEncode(message);    
  HTTPClient http;
  http.begin(url);

  // Specify content-type header
  http.addHeader("Content-Type", "application/x-www-form-urlencoded");
  
  // Send HTTP POST request
  int httpResponseCode = http.POST(url);
  if (httpResponseCode == 200){
    Serial.print("Message sent successfully");
  }
  else{
    Serial.println("Error sending the message");
    Serial.print("HTTP response code: ");
    Serial.println(httpResponseCode);
  }

  // Free resources
  http.end();
}

class Light {
  public:
    int ledPin;
    bool ledOn;

    Light(int number_pin) {
      ledPin = number_pin;
      ledOn = false;
    }

    void Update() {
      ledOn = !ledOn;
      digitalWrite(ledPin, ledOn);
    }
};


class Simulation {
  public:
    enum States {await, setting, pending, alarm};
    unsigned long curr_time = 0;
    int state = AWAIT;
    bool cardPresent = false;
    
    Simulation() {

    }
};

MFRC522 rfid(SS_PIN, RST_PIN);
Light l1(LED_PIN);
Simulation simulation();
// 0 is await
// 1 is setting
// 2 is pending
// 3 is alarm
int state = AWAIT;
unsigned long reference_UID = NULL;
unsigned long curr_time = 0;

// Button initialization
OneButton btn = OneButton(
  BUTTON_PIN,  // Input pin for the button
  true,      
  true
);

// Handler function for a single click:
static void handleClick() {
  state = AWAIT;
}

static void doubleClick() {
  state = SETTING;
}

void setup() {
  Serial.begin(9600);

  WiFi.begin(ssid, password);
  Serial.println("Connecting");
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Connected to WiFi network with IP Address: ");
  Serial.println(WiFi.localIP());

  SPI.begin(); // init SPI bus
  rfid.PCD_Init(); // init MFRC522

  // Single Click event attachment
  btn.attachClick(handleClick);

  // Double Click event attachment with lambda
  btn.attachDoubleClick(doubleClick);

  pinMode(BUZZER_PIN,OUTPUT);
}

unsigned long getID() {
  unsigned long hex_num;
  hex_num =  rfid.uid.uidByte[0] << 24;
  hex_num += rfid.uid.uidByte[1] << 16;
  hex_num += rfid.uid.uidByte[2] <<  8;
  hex_num += rfid.uid.uidByte[3];
  return hex_num;
}

unsigned long getRFID() {
  if (rfid.PICC_IsNewCardPresent()) { // new tag is available
    if (rfid.PICC_ReadCardSerial()) { // NUID has been readed
      MFRC522::PICC_Type piccType = rfid.PICC_GetType(rfid.uid.sak);

      unsigned long ID = getID();
      byte bufferATQA[2];
      byte bufferSize = sizeof(bufferATQA);
      rfid.PICC_WakeupA(bufferATQA, &bufferSize);
      rfid.PCD_StopCrypto1(); // stop encryption on PCD

      return ID;
    }
  }
  return NULL;
}

void setRFID() {
  reference_UID = getRFID();
}

void checkRFID() {
  unsigned long ID = getRFID();
  // if the RFID is not in the wallet
  // prevents freaking out from added card
  if (ID != reference_UID && reference_UID != 0) {
    if (state == AWAIT) {
      state = PENDING;
      curr_time = millis();
    }
  }
  else {
    state = AWAIT;
  }
}

void checkTime() {
  if((millis() - curr_time) > 3000) {
    state = ALARM;
    curr_time = millis();
  }
}

void soundAlarm() {
  sendMessage("Your card is missing!");
}


void loop() {
  // Activate button
  btn.tick();

  // if click once - then await mode -> finished setting or alarm
  // if click twice - then setting mode
  // Need to make sure that the buzzer doesn't constantly go off
  switch (state) {
    case AWAIT:
      checkRFID();
      break;
    case SETTING:
      // Should read the current card in the wallet/on the RFID and store it
      setRFID();
      break;
    case PENDING:
      // Check whether we've detected the OG card yet
      checkRFID();
      // Check the time situation
      checkTime();
      break;
    case ALARM:
      // sound the alarm - until deactivated
      soundAlarm();
      checkRFID();
      break;
  }
}
    
</code></pre>

<h3 style="font-weight: bold;">Week 7 Update: MVP</h3>
<p>For the MVP of this project, I decided to focus on the code logic. I added two new inputs/outputs in addition to the RFID sensor: the button and the buzzer. For the button, I used <a href="https://github.com/mathertel/OneButton">OneButton Library</a> to handle double clicks and single clicks so that the wallet can toggle between setting the card wallet and returning the device to AWAIT state, respectively. For the buzzer, I used previous labs to set the tone for the buzzer when I want to alert that a card has been removed from the wallet for an extended duration.</p>

<p>Regarding the code structure, I set the states as I had outlined before by setting global variables to map each state to an integer for code clarity. After setting up all the inputs/outputs, in the infinite loop, I check which state it is and then direct the code to run appropriate helper functions. When the code is in the SETTING stage, it checks the card on the RFID and stores it as the reference ID that the device will look out for. When the code is in the AWAIT stage, it constantly checks if the card on the ID is the same as the reference ID. If it's not, it sets itself to the PENDING stage and starts a timer. If the timer elapses beyond the prespecified time, the device then moves to the ALARM stage. At this point, the buzzer is activated and the only way to silence it is by clicking the button or replacing the card, both of which returns the state to AWAIT.</p>

<p>One issue I ran into but moved aside for this week was the difficulty for the RFID sensor to read multiple cards at once. The RFID is only able to take in one card at a time which makes detecting the state and the reference a bit challenging. Since the RFID sensor can only be dominated by one card at a time, it was hard to reason about how to handle a wallet full of cards. Therefore, for this iteration, I only focus on handling one card in the wallet.</p>

<p>The setup and the current demo is included below: </p>
<!-- <image width="25%" height="75%" src="./07_mvp/mvp.jpg" alt="MVP"></image> -->

<div class="c">
  <image width="25%" height="100%" src="./07_mvp/mvp.jpg"></image> 
  <video width="25%" height="100%" controls><source src="./07_mvp/MVP_Demo.mp4" type="video/mp4"></video>
</div>

<p>The code I have as of now is below:</p>
<pre><code>
#include &lt;SPI.h&gt;
// https://arduinogetstarted.com/tutorials/arduino-rfid-nfc
#include &lt;MFRC522.h&gt;
// https://github.com/mathertel/OneButton
#include &lt;OneButton.h&gt;

#define SS_PIN 10
#define RST_PIN 5
#define LED_PIN 2
#define BUTTON_PIN 4
#define BUZZER_PIN 2
#define AWAIT 0
#define SETTING 1
#define PENDING 2
#define ALARM 3

class Light {
  public:
    int ledPin;
    bool ledOn;

    Light(int number_pin) {
      ledPin = number_pin;
      ledOn = false;
    }

    void Update() {
      ledOn = !ledOn;
      digitalWrite(ledPin, ledOn);
    }
};

MFRC522 rfid(SS_PIN, RST_PIN);
Light l1(LED_PIN);
int state = AWAIT;
unsigned long reference_UID = NULL;
unsigned long time = 0;

// Button initialization
OneButton btn = OneButton(
  BUTTON_PIN,  // Input pin for the button
  true,      
  true
);

// Handler function for a single click:
static void handleClick() {
  state = AWAIT;
  Serial.println("Clicked!");
}

static void doubleClick() {
  state = SETTING;
  Serial.println("Double Pressed!");
}

void setup() {
  Serial.begin(9600);
  SPI.begin(); // init SPI bus
  rfid.PCD_Init(); // init MFRC522

  // Single Click event attachment
  btn.attachClick(handleClick);

  // Double Click event attachment with lambda
  btn.attachDoubleClick(doubleClick);

  pinMode(BUZZER_PIN,OUTPUT);
}

unsigned long getID() {
  unsigned long hex_num;
  hex_num =  rfid.uid.uidByte[0] << 24;
  hex_num += rfid.uid.uidByte[1] << 16;
  hex_num += rfid.uid.uidByte[2] <<  8;
  hex_num += rfid.uid.uidByte[3];
  return hex_num;
}

unsigned long getRFID() {
  if (rfid.PICC_IsNewCardPresent()) { // new tag is available
    if (rfid.PICC_ReadCardSerial()) { // NUID has been readed
      MFRC522::PICC_Type piccType = rfid.PICC_GetType(rfid.uid.sak);

      // print UID in Serial Monitor in the hex format
      Serial.print("UID:");

      unsigned long ID = getID();
      byte bufferATQA[2];
      byte bufferSize = sizeof(bufferATQA);
      rfid.PICC_WakeupA(bufferATQA, &bufferSize);
      rfid.PCD_StopCrypto1(); // stop encryption on PCD

      return ID;
    }
  }
  return NULL;
}

void setRFID() {
  reference_UID = getRFID();
  
  Serial.print("CHANGING REFERENCE RIGHT HERE RIGHT NOW !!! reference");
  Serial.println(reference_UID);
}

void checkRFID() {
  unsigned long ID = getRFID();
  // if the RFID is not in the wallet
  if (ID != reference_UID && reference_UID != 0) {
    if (state == AWAIT) {
      state = PENDING;
      time = millis();
    }
  }
  else {
    state = AWAIT;
  }
}

void checkTime() {
  if((millis() - time) > 3000) {
    state = ALARM;
    time = millis();
  }
}

void soundAlarm() {
  tone(BUZZER_PIN, 262);
}

void loop() {
  // Activate button
  btn.tick();

  // if click once - then await mode -> finished setting or alarm
  // if click twice - then setting mode
  // Need to make sure that the buzzer doesn't constantly go off
  noTone(BUZZER_PIN);
  switch (state) {
    case AWAIT:
      Serial.println("Awating!!");
      checkRFID();
      break;
    case SETTING:
      // Should read the current card in the wallet/on the RFID and store it
      Serial.println("setting!!");
      setRFID();
      break;
    case PENDING:
      // Check whether we've detected the OG card yet
      checkRFID();
      // Check the time situation
      checkTime();
      break;
    case ALARM:
      // sound the alarm - until deactivated
      soundAlarm();
      checkRFID();
      break;
  }
}
</code></pre>
<p>Moving forward, I want to move all of these helper functions into a Simulation class just for code clarity.</p>

<p>Since the only output for this device is the buzzer, I decided to use the scope to understand how the buzzer worked. When I plotted it, the signal was very noisy. However, it has a fixed clock at 440 microseconds. It seems that the voltage wavers from 0 V to a little over 1 V. The images of the output is below:

  <div class="c">
    <image width="25%" height="100%" src="./07_mvp/Scope_2.jpg"></image>
    <image width="25%" height="100%" src="./07_mvp/Scope_1.jpg"></image> 
  </div>

<h3 style="font-weight: bold;">Week 5 Update: 3D Model, Timeline, and Materials</h3>
<p>For this week, I started thinking about the 3D model for my final project and how I envision it. Because scale will likely be a challenge, I'll just make use of a larger "wallet" with a spring and the RFID attached to the far end so that the RFID sensor is pressed against the cards in the wallet.</p> 
<image width="30%" height="20%" src="./final_proj/3d_sketch.png" alt="3D Sketch"></image>
<p>In the sketch above, I anticipate using the extra space to attach my battery/Arduino board and button. The battery and Arduino board are so that I can program features to the RFID sensor. The button is so that I can change the various states of this device such as "await/in use" and "set cards" which are visualized below. I plan to just use one button but program various types of clicks like double clicks and long presses.</p>
<image width="50%" height="20%" src="./final_proj/state_diagram.jpeg" alt="State Diagram"></image>
<p>I also compiled a list of materials that I'll need:</p>
<ul>
<li>Box</li>
<li>Spring</li>
<li>RFID Sensor</li>
<li>Radio to notify the phone</li>
<li>Button to change between states</li>
<li>Battery for Arduino</li>
</ul>


<p>The timeline I want to reference is the following. I anticipate delays so have left the last few weeks as unscheduled:</p> 
<ul>
    <li>Week 6: Figure out the logic for detecting when a card is removed on Arduino</li>
    <li>Week 7: Create the box with the spring</li>
    <li>Week 8: Create mounts for the battery, button, Arduino board, and RFID sensor</li>
    <li>Week 9: Code out the functionality to notify the phone</li>
    <li>Week 10: Code out the setting stage and when to activate the general use/await on the button</li>
    <li>Week 11: Clean up the code so that the full states are correctly linked</li>
</ul>

<p>I've also began setting up the RFID sensor and used <a href="https://arduinogetstarted.com/tutorials/arduino-rfid-nfc">this</a> as a reference. I'm able to detect the UID of cards and have expanded the starter code to print the UID of the cards until I remove the card. My current code snippet is the following:</p>
<pre><code>
    #include <SPI.h>
    #include <MFRC522.h>
    
    #define SS_PIN 10
    #define RST_PIN 5
    
    MFRC522 rfid(SS_PIN, RST_PIN);
    bool cardPresent = false;
    
    void setup() {
      Serial.begin(9600);
      SPI.begin(); // init SPI bus
      rfid.PCD_Init(); // init MFRC522
    
      Serial.println("Tap RFID/NFC Tag on reader");
    }
    
    void loop() {
      if (rfid.PICC_IsNewCardPresent()) { // new tag is available
        if (!cardPresent) {
          cardPresent = true;
          Serial.println("Card detected.");
        }
        if (rfid.PICC_ReadCardSerial()) { // NUID has been readed
          MFRC522::PICC_Type piccType = rfid.PICC_GetType(rfid.uid.sak);
    
          // print NUID in Serial Monitor in the hex format
          Serial.print("UID:");
          for (int i = 0; i < rfid.uid.size; i++) {
            Serial.print(rfid.uid.uidByte[i] < 0x10 ? " 0" : " ");
            Serial.print(rfid.uid.uidByte[i], HEX);
          }
          Serial.println();
    
          // rfid.PICC_HaltA(); // halt PICC
          byte bufferATQA[2];
          byte bufferSize = sizeof(bufferATQA);
          rfid.PICC_WakeupA(bufferATQA, &bufferSize);
          rfid.PCD_StopCrypto1(); // stop encryption on PCD
        }
      }
      else {
        if (cardPresent) {
          Serial.println("Card removed.");
          cardPresent = false;
        }
      }
    }
    
  </pre></code>
</xmp>


</body>

<script src="./strapdown.js"></script>
<script src="https://cdn.jsdelivr.net/npm/bootstrap@5.0.2/dist/js/bootstrap.bundle.min.js" ></script>

</html>